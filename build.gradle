plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id "org.jetbrains.kotlin.plugin.serialization" version "1.5.10"
    id("io.qameta.allure") version "2.8.1"
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:0.46.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
    implementation "org.hamcrest:hamcrest:2.2"
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2")
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'io.github.serpro69:kotlin-faker:1.11.0'
    testImplementation("io.qameta.allure:allure-junit5:2.14.0")
    implementation("io.qameta.allure:allure-okhttp3:2.10.0")
    implementation 'com.google.code.gson:gson:2.9.0'
    testImplementation 'org.junit.platform:junit-platform-suite-engine:1.8.2'
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5:1.5.20")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation("io.rest-assured:kotlin-extensions:4.4.0")
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

allure {
    version = "2.14.0"
    downloadLinkFormat = "https://repo.maven.apache.org/maven2/io/qameta/allure/allure-commandline/2.14.0/allure-commandline-2.14.0.zip"
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "short"

        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }

        info.events = ["failed", "skipped"]
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "src/**/*.kt"
}